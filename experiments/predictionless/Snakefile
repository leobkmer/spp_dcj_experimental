import os
import pandas as pd

# CONFIGURATION FILE ---------------------------------------------------------
configfile: "config.yaml"
# ----------------------------------------------------------------------------

SAMPLES = config["samples"]
SCRIPT_DIR          = config.get("script_dir", "../../scripts")
INPUT = config.get("input", "input")
OUTPUT = config.get("output", "output")
GUROBI_PYTHON = config.get("gurobipy","python3")
ILP_THREADS = config.get("ilp_threads",10)
ILP_TIMELIM = config.get("ilp_timelim",3600)

def get_ranges(sample):
    if os.path.exists(INPUT+"/ranges"):
        return INPUT+"/ranges/{sample}_range.tsv"
    else:
        return OUTPUT+"/mranges/{sample}.txt"

def get_mem(sample):
    rates = pd.read_csv(INPUT + "/avg_indel_rtt_rates.tsv", sep="\t")
    rate = rates[rates["clade"]==sample]["rate"].values[0]
    if rate>0.7:
        return 65000
    else:
        return 35000

def convert_time(time_ilp): #given in seconds
    time = time_ilp + 600
    hours = time // 3600
    minutes = (time % 3600) // 60
    seconds = time % 60
    time_str = f"{hours:02}:{minutes:02}:{seconds:02}"
    return time_str


rule all_res_adj:
    input:
         expand(OUTPUT+'/res_adj/{sample}.txt',sample=SAMPLES)

rule all_ilp_sol:
    input:
        expand(OUTPUT+"/sol/{sample}.sol",sample=SAMPLES)

rule all_adj:
    input:
        expand(OUTPUT+"/full_adj/{sample}.txt",sample=SAMPLES)

rule all_trees:
    input:
        expand(OUTPUT+"/cptrees/{sample}.txt",sample=SAMPLES)

rule all_ilp:
    input:
        expand(OUTPUT+"/ilp/{sample}.ilp",sample=SAMPLES)

rule construct_ilp:
    input:
        tree = OUTPUT+"/cptrees/{sample}.txt",
        candAdj = OUTPUT+"/full_adj/{sample}.txt",
        mrange= lambda wildcards: get_ranges(wildcards.sample)
    output:
        ilp = OUTPUT+"/ilp/{sample}.ilp",
        idMap = OUTPUT+"/ilp/{sample}_ids.txt",
        #start = OUTPUT+"/ilp/{sample}_init.sol",
        pei = OUTPUT+'/ilp/{sample}_pei.txt'
    resources:
        mem_mb=lambda wildcards, attempt: 30000*attempt
    log:
        OUTPUT+"/ilp/{sample}.log",
    shell:
        "%s/spp_dcj.py -a 1 -m {output.idMap} {input.tree} -fmb {input.mrange} "%SCRIPT_DIR+
        "{input.candAdj} --no_telomeres > {output.ilp} --write-phylogeny-edge-ids {output.pei} 2> {log}"


rule get_range:
    input:
        tree = OUTPUT+"/cptrees/{sample}.txt",
        candAdj = OUTPUT+"/full_adj/{sample}.txt"
    output:
        OUTPUT+"/mranges/{sample}.txt"
    shell:
        "python3 %s/apply_full_range.py {input.tree} {input.candAdj} > {output}"%SCRIPT_DIR


rule run_ilp_tree:
    input:
        ilp = OUTPUT+"/ilp/{sample}.ilp",
        tree=OUTPUT+"/ilp/{sample}_pei.txt"
    output:
        OUTPUT+"/sol/{sample}.sol",
    params:
        time_limit = ILP_TIMELIM
    threads:
        ILP_THREADS
    resources:
        mem_mb=lambda wildcards, attempt: get_mem(wildcards.sample)*attempt,
        time = convert_time(ILP_TIMELIM)
    log:
        OUTPUT+"/sol/{sample}.log",
    shell:
        "%s %s/gurobi_tree.py {input.ilp} {output} {input.tree} --timelim {params.time_limit} -t {threads} > {log}"%(GUROBI_PYTHON,SCRIPT_DIR)


rule sol2adjacencies:
    input:
        sol=OUTPUT+"/sol/{sample}.sol",
        ids=OUTPUT+"/ilp/{sample}_ids.txt",
        pei = OUTPUT+'/ilp/{sample}_pei.txt'
    output:
        adj=OUTPUT+'/res_adj/{sample}.txt',
        eds=OUTPUT+'/res_adj/{sample}_edists.txt'
    shell:
        '%s/sol2adjacencies.py --edge-distances {output.eds} --phylogeny-edge-ids {input.pei} {input.sol} {input.ids} > {output.adj}'%SCRIPT_DIR

rule named_to_tab:
    input:
        INPUT+"/relabelled_trees/{sample}.nw"
    output:
        OUTPUT+"/cptrees/{sample}.txt"
    shell:
        "%s/nwk2tabular.py {input} > {output}"%SCRIPT_DIR


rule clean_unimog:
    input:
        INPUT+"/unimogs/{sample}.unimog"
    output:
        OUTPUT+"/unimogc/{sample}.unimog"
    shell:
        "python3 %s/clean_plasmid_unimog.py {input} > {output}"%SCRIPT_DIR

rule leaves_to_adj:
    input:
        OUTPUT+"/unimogc/{sample}.unimog"
    output:
        OUTPUT+"/leaf_adj/{sample}.txt"
    shell:
        "%s/unimog2adjacencies.py {input} > {output}"%SCRIPT_DIR

rule add_all_adj:
    input:
        adj=OUTPUT+"/leaf_adj/{sample}.txt",
        tree=OUTPUT+"/cptrees/{sample}.txt",
        mranges=lambda wildcards: get_ranges(wildcards.sample)
    output:
        OUTPUT+"/full_adj/{sample}.txt"
    log:
        OUTPUT+"/full_adj/{sample}.log"
    shell:
        "%s/add_all_adjacencies_inner.py {input.tree} {input.adj} {input.mranges} > {output} 2> {log}"%SCRIPT_DIR
